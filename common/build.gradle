architectury {
    common(rootProject.enabled_platforms.split(","))
}

loom {
    accessWidenerPath = file("src/main/resources/ad_astra.accesswidener")
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

//    modImplementation "vazkii.patchouli:Patchouli-xplat:$rootProject.minecraft_version-$rootProject.patchouli_version"
    modImplementation "vazkii.patchouli:Patchouli-xplat:1.19.2-$rootProject.patchouli_version"

    modCompileOnly "mezz.jei:jei-$rootProject.minecraft_version-common-api:$rootProject.jei_version"
    modRuntimeOnly "mezz.jei:jei-$rootProject.minecraft_version-common:$rootProject.jei_version"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api:$rootProject.rei_version"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin:$rootProject.rei_version"

    modApi "com.teamresourceful.resourcefullib:resourcefullib-common-$rootProject.minecraft_version:$rootProject.resourcefullib_version"
    modApi "com.teamresourceful.resourcefulconfig:resourcefulconfig-common-$rootProject.minecraft_version:$rootProject.resourcefulconfig_version"
    modApi "earth.terrarium:botarium-common-$rootProject.minecraft_version:$rootProject.botarium_version"
    modImplementation "software.bernie.geckolib:geckolib-fabric-1.19.3:${rootProject.geckolib_fabric_version}"
}

processResources {
    inputs.property "version", rootProject.version
    inputs.property "resourcefullib_version", rootProject.resourcefullib_version
    inputs.property "resourcefulconfig_version", rootProject.resourcefulconfig_version
    inputs.property "botarium_version", rootProject.botarium_version
}

def ENV = System.getenv()

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact remapJar
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }

            artifactId = "$rootProject.mod_id-common-$rootProject.minecraft_version"

            pom {
                name = 'Ad Astra'
                description = 'Live long and prosper, Ad Astra!'
                url = 'https://github.com/terrarium-earth/Ad-Astra'

                scm {
                    connection = 'git:https://github.com/terrarium-earth/Ad-Astra.git'
                    developerConnection = 'git:https://github.com/terrarium-earth/Ad-Astra.git'
                    url = 'https://github.com/terrarium-earth/Ad-Astra'
                }

                licenses {
                    license {
                        name = 'Terrarium Licence'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://maven.resourcefulbees.com/repository/terrarium/"
            credentials {
                username ENV.MAVEN_USER
                password ENV.MAVEN_PASS
            }
        }
    }
}